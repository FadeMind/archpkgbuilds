# Based on linux-firmware by Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Victor Dmitriyev <mrvvitek@gmail.com>
# Contributor: xduugu

pkgname=linux-firmware-git
pkgver=20160516.80d463b
pkgrel=1
pkgdesc="Firmware files for Linux. Git version"
makedepends=('bash' 'git')
arch=('any')
url="https://git.kernel.org/cgit/linux/kernel/git/firmware/${pkgname/-g/.g}"
license=('GPL2' 'GPL3' 'custom')
conflicts=("${pkgname/-git/}")
provides=("${pkgname/-git/}=${pkgver}")
options=('!strip')
source=("${pkgname}::git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/${pkgname/-g/.g}")
md5sums=('SKIP')

pkgver() {
    # Mimics ABS pkgver described like this:
    # Commit date + git rev-parse --short origin/master
    cd ${pkgname}
    (
    git show --format='%cI' -q master | sed 's/T.*//g;s/-//g'
    echo .
    git rev-parse --short master
    ) | tr -d '\n'
}

package() {
    cd ${pkgname}

    make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install
    rm "${pkgdir}/usr/lib/firmware/"{Makefile,README,configure,GPL-3}

    install -d "${pkgdir}/usr/share/licenses/${pkgname/-git/}"
    install -Dm644 LICEN* WHENCE "${pkgdir}/usr/share/licenses/${pkgname/-git/}/"

    # Trigger a microcode reload for configurations not using early updates
    install -d "${pkgdir}/usr/lib/tmpfiles.d"
    bash -c "{
        echo \"w /sys/devices/system/cpu/microcode/reload - - - - 1\"
        } > ${pkgdir}/usr/lib/tmpfiles.d/${pkgname/-git/}.conf"
}
