# Contributor: FadeMind <fademind@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel-git
pkgver=2.99.917+656+g8477615
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
url="https://01.org/linuxgraphics"
license=('custom')
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers, master git version"
depends=('mesa-dri' 
         'libxvmc' 
         'pixman' 
         'xcb-util>=0.3.9')
makedepends=('xorg-server-devel' 
             'X-ABI-VIDEODRV_VERSION=20' 
             'libx11' 
             'libxrender' 
             'libxrandr' 
             'libxinerama' 
             'libxcursor' 
             'libxtst' 
             'libxss' 
             'git')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output'
            'libxss: for intel-virtual-output')
replaces=('xf86-video-intel' 
          'xf86-video-intel-uxa' 
          'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 
          'xf86-video-intel-sna')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<20' 'X-ABI-VIDEODRV_VERSION>=21' 
           'xf86-video-intel-sna' 
           'xf86-video-intel-uxa'
           'xf86-video-i810' 
           'xf86-video-intel-legacy' 
           'xf86-video-intel')
groups=('xorg-drivers' 'xorg')
install="${pkgname/-git/}.install"
source=('git://anongit.freedesktop.org/xorg/driver/xf86-video-intel')
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname/-git/}
    git describe --long | sed 's/-/+/g'
}

prepare() {
    cd ${pkgname/-git/}
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd ${pkgname/-git/}
    ./configure --prefix=/usr --libexecdir=/usr/lib #--enable-debug=full
    make
}

check() {
    cd ${pkgname/-git/}
    make check
}

package() {
    cd ${pkgname/-git/}

    make DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
