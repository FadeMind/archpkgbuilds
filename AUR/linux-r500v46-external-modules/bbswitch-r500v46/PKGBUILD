# Maintainer: Distorted <derhaeg_michiel@hotmail.com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Boyan Ding <stu_dby@126.com>

_realname=bbswitch
_extramodules=extramodules-4.6-r500v46 # Don't forget to update bbswitch-4.6-r500v46.install
pkgname=bbswitch-r500v46
pkgver=0.8
pkgrel=1
pkgdesc="Kernel module allowing to switch dedicated graphics card on Optimus laptops for linux-r500v46"
arch=('i686' 'x86_64')
url="http://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
depends=('linux-r500v46>=4.6' 'linux-r500v46<4.7' 'xz')
makedepends=('linux-r500v46-headers>=4.6' 'linux-r500v46-headers<4.7')
conflicts=('bbswitch-dkms')
provides=('bbswitch')
install=${pkgname}.install
source=("v${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
md5sums=('5b116b31ace3604ddf9d1fc1f4bc5807')

build() {
    cd ${srcdir}/${_realname}-${pkgver}
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    make KDIR=/lib/modules/${_kernver}/build
}

package() {
    cd ${srcdir}/${_realname}-${pkgver}
    install -Dm644 bbswitch.ko "${pkgdir}"/usr/lib/modules/${_extramodules}/bbswitch.ko
    xz -T 0 -7 "${pkgdir}/usr/lib/modules/${_extramodules}/bbswitch.ko"
}
