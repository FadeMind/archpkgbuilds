# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-r500v
pkgver=367.18
_extramodules=extramodules-4.5-r500v
pkgrel=1
_pkgdesc="NVIDIA drivers for linux-r500v."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-r500v>=4.5' 'linux-r500v<4.6' 'libgl' "nvidia-utils=${pkgver}" 'xz')
makedepends=('linux-r500v-headers>=4.5' 'linux-r500v-headers<4.6')
conflicts=('nvidia-dkms')
license=('custom')
install=nvidia-r500v.install
options=(!strip)
source_i686=("http://us.download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run")
source_x86_64=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
sha256sums_i686=('9a5184308a981b1b36dde4466006b7b45ff4c2a2f364374b3835112899a55dbd')
sha256sums_x86_64=('2e512251203af2ae7d3691046de669b23b62ff3c2940e517940f2e510a93b376')
[[ "$CARCH" = "i686" ]] && _pkg="NVIDIA-Linux-x86-${pkgver}"
[[ "$CARCH" = "x86_64" ]] && _pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia.ko"         "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko"     "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-drm.ko"

    if [[ "$CARCH" = "x86_64" ]]; then
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    fi

    xz -T 0 -7 "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    install -dm755 "${pkgdir}/usr/lib/modprobe.d"

    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-r500v.conf"
}
