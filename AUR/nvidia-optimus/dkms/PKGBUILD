# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)

pkgname=dkms
pkgver=2.2.0.3+git151023
pkgrel=10
pkgdesc='Dynamic Kernel Modules System'
arch=('any')
url='http://linux.dell.com/dkms/'
license=('GPL2')
depends=('bash' 'kmod' 'gcc' 'make' 'patch')
makedepends=('git')
optdepends=('linux-headers: build modules against the Arch kernel'
            'linux-lts-headers: build modules against the LTS kernel'
            'linux-zen-headers: build modules against the ZEN kernel'
            'linux-grsec-headers: build modules against the GRSEC kernel')
backup=('etc/dkms/framework.conf')
install=$pkgname.install
source=('git+git://linux.dell.com/dkms.git#commit=7b6e78f'
        '02-no-kernel-hook.patch'
        'hook.install'
        'hook.remove'
        'hook.sh')
md5sums=('SKIP'
         '82d520c39c99c34977e48b313a189c6c'
         'e6eada07fc3a56a491e14dfeafec746f'
         '57d90c89a221c52d78d0ec3953fe2fcf'
         '11ab1f9d2b602cbf4817abce110681c1')

prepare() {
  cd dkms

  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch $filename"
      patch -p1 -N -i "$srcdir/$filename"
    fi
  done

  # /usr move
  msg2 '/usr move patching'
  for i in dkms{,_framework.conf,.bash-completion,.8,_common.postinst}; do
    sed -ri 's,/lib/modules,/usr/lib/modules,g' "$i"
  done
}

package() {
  # alpm hook
  install -D -m 644 hook.install "$pkgdir/usr/share/libalpm/hooks/70-dkms-install.hook"
  install -D -m 644 hook.remove "$pkgdir/usr/share/libalpm/hooks/70-dkms-remove.hook"
  install -D -m 755 hook.sh "$pkgdir/usr/lib/dkms/alpm-hook"
  # upstream installer
  cd dkms
  make \
    DESTDIR="$pkgdir" \
    SBIN="$pkgdir/usr/bin" \
    BASHDIR="$pkgdir/usr/share/bash-completion/completions" \
    install
}

# vim:set ts=2 sw=2 et:
