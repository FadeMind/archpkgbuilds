# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

###############################################################################################################################
# Issue #699                                                                                                                  #
#-----------------------------------------------------------------------------------------------------------------------------#
# Dirty fix for issue #699 version improved This should fix issue #699 without causing any problems for non-affected users.   #
# It should correctly detect if nvidia and nvidia_modeset are loaded, unload nvidia_modeset using modprobe -r nvidia_modeset  #
# (this also unloads nvidia), and log errors (as before). If it doesn't detect nvidia_modeset but detects "driver",           #
# then it will unload the detected driver just like before, except it does so using modprobe -r "driver" for consistency.     #
# https://github.com/Bumblebee-Project/Bumblebee/issues/699                                                                   #
# https://github.com/arafey/Bumblebee/commit/5636b24fa86a005a5d2e30bd794516db13ccba56                                         #
# https://github.com/arafey/Bumblebee/commit/09d537e8e5313cd0f2c7bf6620ca70454de8a04a                                         #
# https://github.com/arafey/Bumblebee/commit/dbbf20a38aa2bffb10c4e8af583b34dff6bfe721                                         #
# https://github.com/arafey/Bumblebee/commit/bcfe4dd16dd6194f1edbdc53b874a4f408343c5c                                         #
# https://github.com/arafey/Bumblebee/commit/fcfe596eb13f62ca9dd7de272a5a87ae843b2a00                                         #
#                                                                                                                             #
# Patches from arafey are merged in ONE patch 0001-bb_simplify-modules-unload-bug699.patch                                    #
# Reference: https://github.com/Bumblebee-Project/Bumblebee/issues/719                                                        #
#                                                                                                                             #
###############################################################################################################################
# Issue #573                                                                                                                  #
#-----------------------------------------------------------------------------------------------------------------------------#
# Fix devices with a bus larger than 9 (GH-573)                                                                               #
# The `-isolateDevice` option accepts a PCI ID in decimal format, not hex.                                                    #
# https://github.com/Bumblebee-Project/Bumblebee/issues/573                                                                   #
# https://github.com/Bumblebee-Project/Bumblebee/commit/2073f8537412aa47755eb6f3f22a114403e5285b                              #
#                                                                                                                             #
###############################################################################################################################

pkgname=bumblebee
pkgver=3.2.1
pkgrel=11
pkgdesc="NVIDIA Optimus support for Linux through VirtualGL"
arch=('i686' 'x86_64')
depends=('virtualgl' 'glib2' 'mesa-libgl')
makedepends=('help2man')
optdepends=('bbswitch: switch on/off discrete card' 
            'nvidia: NVIDIA kernel driver'
            'nvidia-340xx: NVIDIA kernel driver for older devices'
            'nvidia-304xx: NVIDIA kernel driver for even older devices'
            'primus: faster back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
     optdepends[${#optdepends[@]}]='lib32-primus: faster back-end for optirun'
fi
conflicts=('nvidia-libgl' 'nvidia-340xx-libgl' 'nvidia-304xx-libgl')
provides=('nvidia-libgl' 'nvidia-340xx-libgl' 'nvidia-304xx-libgl')
url="http://www.bumblebee-project.org"
license=("GPL3")
install='bumblebee.install'
backup=('etc/bumblebee/bumblebee.conf' 
        'etc/bumblebee/xorg.conf.nouveau' 
        'etc/bumblebee/xorg.conf.nvidia')
source=("http://www.bumblebee-project.org/${pkgname}-${pkgver}.tar.gz"
        '0001-bb_simplify-modules-unload-bug699.patch'
        '0002-bb-hexadicimal-bug573.patch'
        '0003-bb-mute-bblogger.patch'
)
sha256sums=('1018703b07e2f607a4641249d69478ce076ae5a1e9dd6cff5694d394fa7ee30e'
            'd5f7ca50497c204d19273f0195c5fc609fa01f5de67ba50684e5df59f030b8d3'
            '0b7c1f4bb2e27d131c6c21fd7006d075584917ac4259bd9899e6eca99efc0ece'
            'cbe3e1717bc80146b87d8f2ab1158ee9e094ea5bb2ca9a4a8c09c24b086a7792')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    for p in $(ls ${srcdir}/000{1,2}*.patch); do
        patch -Np1 -i "$p"
    done
    
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-udev-rules=/usr/lib/udev/rules.d/ \
        --sysconfdir=/etc \
        --without-pidfile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    # Install main app
    make install DESTDIR="$pkgdir"
    
    # Blacklist nvidia and nouveau modules
    # Reference: https://github.com/Bumblebee-Project/Bumblebee/issues/719
    install -dm755 ${pkgdir}/usr/lib/modprobe.d
    bash -c "{
    echo \"blacklist nvidia\"
    echo \"blacklist nvidia-drm\"
    echo \"blacklist nvidia-modeset\"
    echo \"blacklist nvidia-uvm\"
    echo \"blacklist nouveau\"
    } > ${pkgdir}/usr/lib/modprobe.d/bumblebee.conf"
    
    # Install systemd unit
    install -Dm644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"    
    sed -i "s/sbin/bin/" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
}
