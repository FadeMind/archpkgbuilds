# Maintainer: Antonio Rojas 

pkgname=okular-frameworks-git
pkgver=r7280.32b62e9
pkgrel=1
pkgdesc='Universal document viewer'
arch=('i686' 'x86_64')
url='https://quickgit.kde.org/?p=okular.git'
license=('GPL')
depends=('chmlib' 'djvulibre' 'kactivities' 'kdelibs4support' 'kdesignerplugin' 'khtml' 'kpty' 'libkexiv2' 'libspectre' 'poppler-qt5' 'threadweaver')
makedepends=('ebook-tools' 'extra-cmake-modules' 'git' 'kdoctools' 'python' 'qca-qt5')
optdepends=('ebook-tools: mobi and epub support' 'qca-qt5: support for encrypted ODF documents')
conflicts=('kdegraphics-okular' 'okular')
provides=('okular')
source=("git://anongit.kde.org/okular.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
    cd okular
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    if [[ -d build ]]; then
        rm -Rf build
    fi
    mkdir -p build
}

build() { 
  cd build
  cmake ../okular \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  echo "NoDisplay=true" >> ${pkgdir}/usr/share/applications/org.kde.mobile.okular.desktop
}
