pkgname=corebird-git
pkgver=4589+gc3e36eb
pkgrel=1
pkgdesc="Native Gtk+ Twitter Client. Git version"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/baedert/${pkgname/-git/}"
depends=('gtk3>=3.14'
        'glib2>=2.40'
        'rest>=0.7'
        'libgee'
        'sqlite3'
        'libsoup>=2.4'
        'json-glib'
        'intltool'
        'gstreamer'
        'gst-plugins-good'
        'gst-plugins-bad'
        'gst-libav'
        'hicolor-icon-theme' 
        'desktop-file-utils')
makedepends=('vala>=0.26' 'git' 'automake')
conflicts=("${pkgname/-git/}")
source=("${pkgname/-git/}::git+${url}")
sha1sums=('SKIP')

pkgver() {
    cd ${pkgname/-git/}
    #git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
    echo $(git rev-list --count HEAD)+g$(git rev-parse --short HEAD)
}

build() {
    cd ${pkgname/-git/}
    # Add --disable-video here if you don't like the gstreamer deps
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd ${pkgname/-git/}
    make DESTDIR=$pkgdir install
}
