# Maintainer: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Maintainer: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt-git
pkgver=20170124
pkgrel=1
pkgdesc="Disk encryption with strong security based on TrueCrypt. Git version."
arch=('i686' 'x86_64')
url="https://github.com/veracrypt/VeraCrypt"
license=('custom')
depends=('fuse' 'wxgtk>=3.0')
makedepends=('bash' 'binutils' 'desktop-file-utils' 'git' 'make' 'nasm' 'pkg-config')
conflicts=("${pkgname%-git}")
provides=("${pkgname%-git}")
replaces=("${pkgname%-git}")
install="${pkgname%-git}.install"
source=("${pkgname%-git}::git+${url}.git"
        "${pkgname%-git}.desktop"
        "${pkgname%-git}.install")
sha256sums=('SKIP'
            '2fadb2c25c28df33d93e98e693774b407c5d1d29b182df5fec8243a3e61f8c0c'
            '61b30fea572fb021febc6a5ff62b38423b3a77b7453abd4daf0c0e9c037cb3ae')

pkgver() {
    cd ${pkgname%-git}
    git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
    cd ${srcdir}/${pkgname%-git}/src
    msg2 "Applying patches..."
    sed -i '/makeself/d' Main/Main.make
}

build() {
    cd ${srcdir}/${pkgname%-git}/src
    msg2 "Building veracrypt..."
    export WX_CONFIG=/usr/bin/wx-config
    make LFLAGS+="-ldl"
}

package() {
    cd ${srcdir}/${pkgname%-git}/src
    install -D -m755 Main/${pkgname%-git} "${pkgdir}/usr/bin/${pkgname%-git}"
    install -D -m644 Resources/Icons/VeraCrypt-48x48.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
    install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
    desktop-file-install ${srcdir}/${pkgname%-git}.desktop --dir ${pkgdir}/usr/share/applications/
    install -d "${pkgdir}"/etc/modules-load.d/
    bash -c "{
    echo \"loop\"
    } > ${pkgdir}/etc/modules-load.d/zulucrypt.conf"
}
