# Maintainer: maz-1 <ohmygod19993 at gmail dot com>

_pkgname=krename
pkgname=$_pkgname-frameworks-git
pkgver=r269.22bcb1e
pkgrel=1
pkgdesc="A very powerful batch file renamer for KDE. KF5 Version."
arch=('i686' 'x86_64')
url="https://github.com/sandsmark/krename"
license=('GPL')
depends=('taglib' 'podofo' 'kjs' 'exiv2' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
options=('libtool')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    if [[ -d build ]]; then
        rm -Rf build
    fi
    mkdir -p build
}

build() {
    cd build
    cmake ../${_pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="-I/usr/include/freetype2"
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
