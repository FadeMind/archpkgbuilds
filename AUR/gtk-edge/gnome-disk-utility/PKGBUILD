# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.20.1
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=('i686' 'x86_64')
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=('udisks2' 'gtk3' 'libsecret' 'libpwquality' 'libcanberra' 'libdvdread' 'libnotify' 'desktop-file-utils' 'parted')
makedepends=('intltool' 'docbook-xsl' 'gnome-settings-daemon')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6877a5b6ea949c9527aac7a1ac38c02b4dd5f8230dc59be5a8f48a0ee381f1cd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
