# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=4.3.1
pkgrel=6.1
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url='http://ktorrent.pwsp.net/'
license=('GPL2')
depends=('libktorrent' 'kdebase-runtime' 'taglib')
makedepends=('automoc4' 'cmake' 'boost' 'kdepimlibs4')
optdepends=('kdepimlibs4: syndication plugin')
install=${pkgname}.install
source=("http://ktorrent.pwsp.net/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'kde412.patch'
        'kdebug-315239.patch'
        'magnetmime.patch')
sha1sums=('6b56bab48dd07805225c3d4d5d5d1c4c1def241e'
          'f2c0e05913b2f21b593818e2cd3547d09f86be86'
          'd247aecbbbbbda3eef8dcb51d3db244b1c845f85'
          'ad6704110558c79600214dab82897a4a02148992')

prepare() {
	mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/kde412.patch
  patch -p1 -i "${srcdir}"/kdebug-315239.patch
  patch -p1 -i "${srcdir}"/magnetmime.patch
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
	# FIX https://bugs.archlinux.org/task/43637
	install -D -m644 ${pkgdir}/usr/share/kde4/services/magnet.protocol \
                         ${pkgdir}/usr/share/kservices5/magnet.protocol
}
