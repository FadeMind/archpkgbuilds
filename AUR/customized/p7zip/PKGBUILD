# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: TuxSpirit<tuxspirit@archlinux.fr>  2007/11/17 21:22:36 UTC
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=p7zip
pkgver=15.14.1
pkgrel=1
pkgdesc="Command-line file archiver with high compression ratio"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/${pkgname}/"
license=('LGPL' 'custom:unRAR')
depends=('gcc-libs' 'sh')
makedepends_i686=('nasm')
makedepends_x86_64=('yasm')
install="${pkgname}.install"
source=("${pkgname}_${pkgver}_src_all.tar.bz2::https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/15.14%20.1/${pkgname}_${pkgver}_src_all.tar.bz2")
sha256sums=('699db4da3621904113e040703220abb1148dfef477b55305e2f14a4f1f8f25d4')

prepare() {
    cd ${pkgname}_${pkgver}

    if [[ $CARCH = x86_64 ]]; then
        cp makefile.linux_amd64_asm makefile.machine
    else
        cp makefile.linux_x86_asm_gcc_4.X makefile.machine
    fi
}

build() {
    cd ${pkgname}_${pkgver}
    make all3 OPTFLAGS="$CFLAGS"
}

package() {
    cd ${pkgname}_${pkgver}

    make install \
        DEST_DIR="$pkgdir" \
        DEST_HOME=/usr \
        DEST_MAN=/usr/share/man

    install -d "${pkgdir}"/usr/share/licenses/p7zip
    ln -s -t "$pkgdir/usr/share/licenses/p7zip/" \
        /usr/share/doc/p7zip/DOC/License.txt \
        /usr/share/doc/p7zip/DOC/unRarLicense.txt

    chmod -R a+r,u+w,a+X "$pkgdir/usr"
}
