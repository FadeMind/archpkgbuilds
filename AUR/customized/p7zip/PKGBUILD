# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: TuxSpirit<tuxspirit@archlinux.fr>  2007/11/17 21:22:36 UTC
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=p7zip
pkgver=16.02
pkgrel=2
pkgdesc="Command-line file archiver with high compression ratio"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/${pkgname}/"
license=('LGPL' 'custom:unRAR')
depends=('gcc-libs' 'sh' 'wxgtk')
makedepends=('desktop-file-utils' 'python' 'webkitgtk2')
makedepends_i686=('nasm')
makedepends_x86_64=('yasm')
install="${pkgname}.install"
conflicts=('p7zip-gui')
source=("${pkgname}_${pkgver}_src_all.tar.bz2::https://freefr.dl.sourceforge.net/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2" '7zFM.desktop')
sha256sums=('5eb20ac0e2944f6cb9c2d51dd6c4518941c185347d4089ea89087ffdd6e2341f'
            '8cb662ccbacd1badc2c41ff00618c53d1c7fb8bca5472cca4ac7bd7f619acb27')

prepare() {
    cd ${pkgname}_${pkgver}

    if [[ $CARCH = x86_64 ]]; then
        cp makefile.linux_amd64_asm makefile.machine
    else
        cp makefile.linux_x86_asm_gcc_4.X makefile.machine
    fi
    
    sed -i 's/x86_64-linux-gnu//g' CPP/7zip/*/*/*.depend
	rm GUI/kde4/p7zip_compress.desktop # FS#43766
	cd Utils
	sed -i 's/_do_not_use//g' generate.py
	./generate.py
}

build() {
    msg2 "Building application..."
    cd ${pkgname}_${pkgver}
    make all3 OPTFLAGS="$CFLAGS"
    make 7zFM 7zG OPTFLAGS="${CFLAGS}"
}

package() {
    cd ${pkgname}_${pkgver}

    msg2 "Installing application..."
    make install DEST_DIR="$pkgdir" DEST_HOME=/usr DEST_MAN=/usr/share/man

    msg2 "Installing desktop integration..."
    install -Dm644 GUI/p7zip_32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/p7zip.png
    install -dm755 ${pkgdir}/usr/share/{applications,doc/p7zip/DOC/MANUAL,licenses/p7zip,kde4/services/ServiceMenus,kservices5/ServiceMenus}
    cp GUI/kde4/* ${pkgdir}/usr/share/kde4/services/ServiceMenus
    cp GUI/kde4/* ${pkgdir}/usr/share/kservices5/ServiceMenus
    ln -s 7zCon.sfx ${pkgdir}/usr/lib/p7zip/7z.sfx
    cp -r DOC/MANUAL/fm ${pkgdir}/usr/share/doc/p7zip/DOC/MANUAL

    ln -s -t "${pkgdir}/usr/share/licenses/p7zip/" \
        /usr/share/doc/p7zip/DOC/License.txt \
        /usr/share/doc/p7zip/DOC/unRarLicense.txt

    msg2 "Fixing file permissions..."
    chmod -R a+r,u+w,a+X "${pkgdir}/usr"
    chmod +x ${pkgdir}/usr/bin/p7zipForFilemanager
    
    desktop-file-install ${startdir}/7zFM.desktop --dir "${pkgdir}/usr/share/applications/"
}
