# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dropbox
pkgver=3.12.6
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("x86_64")
url="http://www.dropbox.com"
license=('custom')
depends=('libxmu' 'libxslt')
makedepends=('chrpath')
optdepends=('ufw-extras: UFW rules for Dropbox'
            'perl-file-mimeinfo: Opening the Dropbox folder on some desktop environments')
conflicts=("dropbox-experimental")
replaces=("dropbox3" "dropbox2")
options=('!strip')
source=("https://dl.${pkgname}usercontent.com/u/17/${pkgname}-lnx.${arch}-${pkgver}.tar.gz" "${pkgname}.desktop" "${pkgname}.png" "terms.txt" "${pkgname}-papirus.zip" "${pkgname}")
sha256sums=('92ca8fb2fc23c128234505ac8f8b9b1073a3cc17e70a92c9ee51ede7577aa099'
            '541f2fd2de0d601a08cde7853e404062f542af21e6e7106825b5e68177168e0f'
            'e7d245f5d1a3d5322614b61400ae2913a8caef44bc86717ff7d8197a15dd7f01'
            'c39d63bcfc0d40002a5df052298feb79f2e8dd3d93da27ef31eb672b8d2fc70e'
            '0929007a898b94456fe3a813b4a0534709e43822a977f90270cce9cd83aa216b'
            '4240052a07a0f38dbdea6f199d5f1fc7b04ace9bf7474a63f09bdff7682a5154')

prepare() {
    cd .dropbox-dist/${pkgname}-lnx.${arch}-${pkgver}
    rm library.zip
    ln -s dropbox library.zip
    chrpath -d plugins/platforms/libqxcb.so
}

package() {
    cd .dropbox-dist

    install -dm755 ${pkgdir}/opt
    cp -dr --no-preserve='ownership' ${pkgname}-lnx.${arch}-${pkgver} "${pkgdir}"/opt/dropbox
    rm -rf ${pkgdir}/opt/dropbox/images/hicolor/
    cp -R ${srcdir}/hicolor/ ${pkgdir}/opt/dropbox/images/
    find "${pkgdir}"/opt/dropbox -type d -exec chmod 755 {} +
    find "${pkgdir}"/opt/dropbox -type f -exec chmod 644 {} +
    chmod 755 "${pkgdir}"/opt/dropbox/dropbox{,d}

    install -dm755 ${pkgdir}/usr/bin
    ln -s /opt/dropbox/dropbox ${pkgdir}/usr/bin/dropbox
    install -Dm755 ${srcdir}/dropbox ${pkgdir}/usr/bin/dropbox

    install -dm 755 ${pkgdir}/usr/share/{applications,licenses/dropbox,pixmaps}
    install -m 644 ../dropbox.desktop ${pkgdir}/usr/share/applications/
    install -m 644 ../dropbox.png     ${pkgdir}/usr/share/pixmaps/
    install -m 644 ../terms.txt       ${pkgdir}/usr/share/licenses/${pkgname}/
}
