# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Yamashita Ren
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

pkgname=peazip-qt-opensuse
pkgver=6.0.2
_rpmrel=3.1
pkgrel=1
pkgdesc="QT archiver utility (openSUSE x86_64 RPM)"
arch=('x86_64')
url='http://peazip.sourceforge.net/peazip-linux.html'
license=('Artistic2.0' 'LGPL2.1')
depends=('curl' 'desktop-file-utils' 'gmp4' 'libx11' 'ncurses' 'qt4pas')
optdepends=('balz' 'lpaq' 'p7zip' 'paq8o' 'quad' 'unace' 'upx' 'rar' 'zpaq')
provides=(${pkgname%-*-*} ${pkgname%-*})
conflicts=(${pkgname%-*-*} ${pkgname%-*-*}-gtk2 ${pkgname%-*})
source=("http://download.opensuse.org/repositories/home:/ecsos/openSUSE_Factory/${arch}/${pkgname%-*-*}-${pkgver}-${_rpmrel}.${arch}.rpm"
        'https://github.com/FadeMind/peazip-additional-formats-plugin/archive/master.zip'
        'http://www.peazip.org/peazip_help.pdf')
sha256sums=('6cde64172121c0cf6ad11f9c2e80367f210b0f21891ce9ef50c236654b0b2c76'
            '32bb0d25c2c746d55c07fd336b443581bdf0d28ffadd7a23b5bb2ad86e0fa5c7'
            '925e9c65f4ae38c120651cdfc6567687770238487655e0bcd4f1e6bf53ca2929')

prepare() {
    # creating lib path
    install -d "$srcdir"/usr/lib
    # removing conflicts
    rm -R "$srcdir"/usr/share/peazip/res/upx
    # moving peazip directory to proper path
    mv -f "$srcdir"/usr/share/peazip "$srcdir"/usr/lib/
}

package() {
    # setting $_respath value
    _respath=usr/lib/peazip/res
    # installing main app directory and copy files
    cp -R "$srcdir"/usr "$pkgdir"/
    cp -R "$srcdir"/peazip-additional-formats-plugin-master/res/* "$pkgdir"/$_respath/
    # looking for p7zip libs and create proper symlinks in $_respath directory
    install -d "$pkgdir/$_respath"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar{,29}.so; do
        ln -sf /usr/lib/p7zip/$i "$pkgdir/$_respath"/7z/$i
    done    
    # looking for external libs and create proper symlinks in $_respath directory
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o zpaq/zpaq; do
        install -d "$pkgdir/$_respath"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$pkgdir/$_respath"/$i
    done
    # creating symlinks for executable files
    ln -sf /$_respath/pea          "$pkgdir"/usr/bin/pea
    ln -sf /$_respath/pealauncher  "$pkgdir"/usr/bin/pealauncher
    ln -sf /usr/lib/peazip/peazip  "$pkgdir"/usr/bin/peazip
    # installing doc files
    mv -f "$pkgdir"/usr/share/doc/packages/peazip "$pkgdir"/usr/share/doc/"$pkgname"/
    rm -R "$pkgdir"/usr/share/doc/packages/
    # installing help file
    install -Dm0644 "$srcdir"/peazip_help.pdf "$pkgdir"/usr/lib/peazip/peazip_help.pdf
    # fix chmod
    find "$pkgdir" -type f -name *.txt -exec chmod 644 {} +
}
