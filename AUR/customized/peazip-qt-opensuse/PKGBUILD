# Contributor: Yamashita Ren
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

pkgname=peazip-qt-opensuse
pkgver=6.0.1
_rpmrel=1.1
pkgrel=1
pkgdesc="QT archiver utility (openSUSE x86_64 RPM)"
arch=(x86_64)
url=http://peazip.sourceforge.net/peazip-linux.html
license=(Artistic2.0 LGPL2.1)
depends=(curl desktop-file-utils gmp4 libx11 ncurses qt4pas)
optdepends=(balz lpaq p7zip paq8o quad unace upx rar zpaq)
provides=(${pkgname%-*-*} ${pkgname%-*})
conflicts=(${pkgname%-*-*} ${pkgname%-*-*}-gtk2 ${pkgname%-*})
source=(http://download.opensuse.org/repositories/home:/ecsos/openSUSE_Factory/${arch}/${pkgname%-*-*}-${pkgver}-${_rpmrel}.${arch}.rpm 
        peazip_additional_formats_plugin-merged.LINUX.ALL.tar.xz
        peazip_help.pdf)
sha256sums=('7ae858362df20cb9850268861b26b853489da725779a097a5227709212b7d1bf'
            'f86b659739dc0c7c9e4365f700fae91677cd290fc61743381d14ab300ccded72'
            '5efdee100d15b114413cc9f419aa9d78889bf1998f1d3060371b9c2bd4ea1d4c')

prepare() {
    # creating lib path
    install -dm755 "$srcdir"/usr/lib/
    # removing conflicts
    rm -R "$srcdir"/usr/share/peazip/res/upx
    # moving peazip directory to proper path
    mv -f "$srcdir"/usr/share/peazip "$srcdir"/usr/lib/
    
}

package() {
    # setting $_respath value
    _respath=usr/lib/peazip/res
    # installing main app directory and copy files
    cp -R "$srcdir"/usr "$pkgdir"/
    cp -R "$srcdir"/res/* "$pkgdir"/$_respath/
    # looking for p7zip libs and create proper symlinks in $_respath directory
    install -d "$pkgdir/$_respath"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar{,29}.so; do
        ln -sf /usr/lib/p7zip/$i "$pkgdir/$_respath"/7z/$i
    done    
    # looking for external libs and create proper symlinks in $_respath directory
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o zpaq/zpaq; do
        install -d "$pkgdir/$_respath"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$pkgdir/$_respath"/$i
    done
    # creating symlinks for executable files
    ln -sf /usr/lib/peazip/res/pea          "$pkgdir"/usr/bin/pea
    ln -sf /usr/lib/peazip/res/pealauncher  "$pkgdir"/usr/bin/pealauncher
    ln -sf /usr/lib/peazip/peazip           "$pkgdir"/usr/bin/peazip
    # installing doc files
    mv -f "$pkgdir"/usr/share/doc/packages/peazip "$pkgdir"/usr/share/doc/"$pkgname"/
    rm -R "$pkgdir"/usr/share/doc/packages/
    # installing help file
    install -Dm0644 "$srcdir"/peazip_help.pdf "$pkgdir"/usr/lib/peazip/peazip_help.pdf
}
