# Maintainer: Alex Taber <aft dot pokemon at gmail dot com>

pkgname=teamviewer
pkgver=11.0.57095
pkgrel=4
pkgdesc='All-In-One Software for Remote Support and Online Meetings'
arch=('i686' 'x86_64')
url='http://www.teamviewer.com'
license=('custom')
options=('!strip')
provides=('teamviewer')
conflicts=('teamviewer-beta')
makedepends=(tar)
depends_x86_64=(lib32-{fontconfig,lib{png12,sm,xinerama,xrender,jpeg6-turbo}})
depends_i686=(fontconfig lib{png12,sm,xinerama,xrender,jpeg6-turbo})
source_x86_64=("http://download.${pkgname}.com/download/version_${pkgver%%.*}x/${pkgname}_${pkgver}_amd64.deb")
source_i686=("http://download.${pkgname}.com/download/version_${pkgver%%.*}x/${pkgname}_${pkgver}_i386.deb")
sha256sums_i686=('a3e763912892019143517f8c40404aa23465b4d78fb382336d165d3b96f1b83d')
sha256sums_x86_64=('db5429634434485d7ce5a6a0bcfb4e37f190b3c2b550ebdec0eb2afba792cb8e')
noextract=(${pkgname}_${pkgver}_{amd64,i386}.deb)
install=teamviewer.install

prepare() {
    echo "If the install fails, you need to uninstall Teamviewer 10"
    tar -xf data.tar.bz2
}

package() {
    # Install
    echo "If the install fails here, you need to uninstall Teamviewer 10"
    cp -dr --no-preserve=ownership {etc,opt,usr,var} "${pkgdir}"/

    # Additional files
    rm "${pkgdir}"/opt/teamviewer/tv_bin/xdg-utils/xdg-email
    install -D -m0644 "${pkgdir}"/opt/teamviewer/tv_bin/script/teamviewerd.service "${pkgdir}"/usr/lib/systemd/system/teamviewerd.service
    install -d -m0755 "${pkgdir}"/usr/{share/applications,share/licenses/teamviewer}
    ln -s /opt/teamviewer/tv_bin/desktop/teamviewer-teamviewer${pkgver%%.*}.desktop "${pkgdir}"/usr/share/applications/teamviewer.desktop
    ln -s /opt/teamviewer/License.txt "${pkgdir}"/usr/share/licenses/teamviewer/LICENSE
}
