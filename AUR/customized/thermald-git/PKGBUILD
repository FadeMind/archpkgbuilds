# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>
# Contributor: rat4 <rat4vier@gmail.com>
# Maintainer: ultraviolet <ultravioletnanokitty@gmail.com>

pkgname=thermald-git
pkgver=1.5.3.r0.g0c4c715
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
makedepends=('git' 'bash')
conflicts=("${pkgname/-git/}")
provides=("${pkgname/-git/}")
install="${pkgname/-git/}.install"
source=("${pkgname/-git/}::git://github.com/01org/thermal_daemon.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname/-git/}
  git describe --tags --long | sed -r -e 's,^[^0-9]*,,;s,([^-]*-g),r\1,;s,[-_],.,g'
}

build() {
    cd ${pkgname/-git/}
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
    make
}

package() {
    cd ${pkgname/-git/}
    make DESTDIR="$pkgdir" install
    mkdir -p ${pkgdir}/etc/{systemd/system/thermald.service.d,modules-load.d}
    # modules load during boot
    bash -c "{
    echo \"msr\"
    echo \"coretemp\"
    } > ${pkgdir}/etc/modules-load.d/thermald.conf"
    # disable debug info in journal logs
    # Reference: https://www.reddit.com/r/archlinux/comments/3okrhl/thermald_anyone/cw0fqmb
    bash -c "{
    echo \"[Service]\"
    echo \"StandardOutput=null\"
    } > ${pkgdir}/etc/systemd/system/thermald.service.d/nostdout.conf"
    # remove upstart
    rm -rf "${pkgdir}/etc/init"
}
