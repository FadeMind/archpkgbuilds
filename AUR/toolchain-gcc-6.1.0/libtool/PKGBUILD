# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=libtool
pkgver=2.4.6
pkgrel=4
_gccver=6.1.0
pkgdesc="A generic library support script"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('base-devel')
depends=('sh' 'tar' 'glibc')
makedepends=("gcc=$_gccver")
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver")
conflicts=('libltdl' 'libtool-multilib')
replaces=('libltdl' 'libtool-multilib')
install=libtool.install
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz)

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
}
sha256sums=('7c87a8c2c8c0fc9cd5019e402bed4292462d00a718a7cd5f11218153bf28b26f')
